#!/bin/tcsh
#Tyler Blazey, 2011.

#######
#Usage#
#######

set program = $0:t
if ($#argv == 0) then
	echo ""
	echo "${program}: A basic wrapper for FSL's field map tools"
	echo ""
	echo "Usage:"
	echo ""
	echo "Required Arguments:"
	echo "	-m: field map magnitude image"
	echo "	-p: field map phase image"
	echo "	-e: EPI to unwarp."
	echo "	-dwell: EPI dwell time. In milliseconds"
	echo "	-te: EPI echo time. In milliseconds"
	echo "	-delta: Difference between field map echo times. In milleseconds."
	echo ""
	echo "Optional Arguments:" 
	echo "	-reg: Field map regulation. Takes despike, smooth, median, or none. Default is despike."
	echo "	-frame:	EPI frame to extract for registration. Default is 0."
	echo "	-o: Outpath. Default is unwarp"
	echo "	-r: Outroot. Default is blank"
	echo "	-thresh: Signal loss threshhold. Default is 0.1"
	echo "	-dthresh: Despike threshhold. For use with -reg despike. Default is 2.1"
	echo " 	-mm3: Size of kernel to use with -reg smooth. Default is 5"
	echo " 	-dir: Direction of epi unwarp. Default is y-. Takes x,x-,y,y-,z,z-"
	echo "	-nounwrap: Turn off phase unwrapping. On by default."
	echo "	-dtimc: Due eddy correction for dti input. Will only resample once."
	echo "	-boldmc: Apply mcflirt to bold input. Will only resample once."
	echo ""
	echo "Tips:"
	echo "	Standard CAP DTI parameters:  Dwell Time=.98ms, Echo Time=112ms."
	echo "	Standard CAP BOLD parameters: Dwell Time=.47ms, Echo Time=27ms."
	echo "	Standard CAP Field Map delta TE=2.46ms."
	echo ""	
	exit
endif


#################
#Parse Arguments#
#################

while ($#argv > 0) 
	set flag = $argv[1]; shift;
	switch ($flag)
		case -m:
			set mag = $argv[1]; shift; breaksw;
		case -e:
			set epi = $argv[1]; shift; breaksw;
		case -p:
			set field = $argv[1]; shift; breaksw;
		case -o:
			set outpath = $argv[1]; shift; breaksw;
		case -r:
			set outroot = "$argv[1]_"; shift; breaksw;
		case -te:
			set te = `echo "$argv[1]/1000" | bc -l`; shift; breaksw;
		case -dwell:
			set dwell = `echo "$argv[1]/1000" | bc -l`; shift; breaksw;
		case -delta:
			set delta = `echo "$argv[1]/1000" | bc -l`; shift; breaksw;
		case -thresh
			set thresh = `echo "1-$argv[1]" | bc -l` ; shift; breaksw;
		case -frame:
			set frame = $argv[1]; shift; breaksw;
		case -reg:
			set reg = $argv[1]; shift; breaksw;
		case -dthresh:
			set dthresh = $argv[1]; shift; breaksw;
		case -mm3:
			set mm3 = $argv[1]; shift; breaksw;
		case -dir:
			set dir = $argv[1]; shift; breaksw;
		case -nounwrap:
			set unwrap = 0; breaksw;
		case -dtimc:
			set dtimc = 1; breaksw;
		case -boldmc
			set boldmc = 1; breaksw;
		default:
			echo "Option $flag not recognized. See usage."
			exit 1
	endsw
end

#############################
#Check options, set defaults#
#############################

#Check to make sure all required options have been set
if ( $?mag == 0 || $?epi == 0 || $?field == 0  || $?dwell == 0 || $?te == 0 || $?delta == 0  ) then
	echo "Missing required arguments. See usage"
	exit 1
endif

#Set defaults
if ($?frame == 0)	set frame = 0; 
if ($?outroot == 0)	set outroot = ""
if ($?thresh == 0)	set thresh = 0.9
if ($?outpath == 0) set outpath = "unwarp"
if (! -d $outpath)	mkdir $outpath
if ($?reg == 0) set reg = "despike"
if ($reg == "despike" && $?dthresh == 0) set dthresh = 2.1
if ($reg == "smooth" && $?mm3 == 0) set mm3 = 5
if ($?dir == 0) set dir = "y-"
if ($?unwrap == 0) set unwrap = 1

#Check to make sure -reg is set correctly
set reg_options = ( "smooth" "median" "none" "despike"); echo $reg_options | grep -qw $reg;
if ( $? == 1 ) then
	echo "-reg option $reg not known. See usage"
	exit 1
endif

#Check to see if -dir is set correctly
set dir_options = ( "x" "x-" "y" "y-" "z" "z-"); echo $dir_options | grep -qw $dir;
if ( $? == 1 ) then
	echo "-dir option $dir not known. See usage"
	exit 1
endif

#####################################
#Preproc, Unit Conversion, Unwraping#
#####################################
set echo
#Copy images over
imcp $mag ${outpath}/${outroot}mag; imcp $epi ${outpath}/${outroot}epi
imcp $field ${outpath}/${outroot}field;
cd $outpath

#Check to see if the field map range is as expected
set range = `fslstats ${outroot}field -R`.
if ( $range[1] != -4096.000000 || $range[2] != 4092.000000 ) then
	echo "Range of field map is not -4096.000000 to 4092.000000. Exiting..."
	exit 1
endif

#Convert field map to radians
fslmaths ${outroot}field -div 4096 -mul 3.14159265 ${outroot}field

#If user wants, unwrap phase with prelude.
if ($unwrap == 1) prelude -p ${outroot}field -a ${outroot}mag -o ${outroot}field

#Convert to radians/second
fslmaths ${outroot}field -div $delta ${outroot}field

###############
#Brain Masking#
###############

#Extract epi frame, then brain extract
fslroi ${outroot}epi ${outroot}epi_${frame} $frame 1
bet ${outroot}epi_${frame} ${outroot}epi_${frame}_brain -m -f .2 -R

#Create brain mask for magnitude
bet ${outroot}mag ${outroot}mag_brain -m -R

#Create inverted field map brain mask
fslmaths ${outroot}field -abs -bin -mas ${outroot}mag_brain_mask -mul -1 -add 1 -bin \
		 ${outroot}field_inv_brain_mask
		 
#Cluster inverted brain mask
cluster -i ${outroot}field_inv_brain_mask -t 0.5 --no_table \
		-o ${outroot}field_inv_brain_mask_clust
		
#Save intensity of largest cluster 
set max = `fslstats ${outroot}field_inv_brain_mask_clust -R | awk '{print $2}'`
#Threshhold the image by max, then invert again. Get a new, tighter brain mask.
fslmaths ${outroot}field_inv_brain_mask_clust -thr $max -bin -mul -1 -add 1 -bin \
		 -mas ${outroot}mag_brain_mask ${outroot}mag_brain_mask
		 
#Use the new brain mask on the field map
fslmaths ${outroot}field -mas ${outroot}mag_brain_mask ${outroot}field

#Get a 50% brain mask
set fifty = `fslstats ${outroot}mag_brain -P 98 | awk '{print ( $1 / 2 ) }'`
fslmaths ${outroot}mag_brain -thr $fifty -bin ${outroot}mag_brain_fifty_mask

#Erode the original brain mask
fslmaths ${outroot}mag_brain_mask -ero ${outroot}mag_brain_mask_eroded

#Add eroded and fifty masks
fslmaths ${outroot}mag_brain_mask_eroded -add ${outroot}mag_brain_fifty_mask -thr 0.5 \
		 -bin ${outroot}mag_brain_mask
		 
#Mask the field map again
fslmaths ${outroot}field -mas ${outroot}mag_brain_mask ${outroot}field

#Erode brain mask again
fslmaths ${outroot}mag_brain_mask -ero ${outroot}mag_brain_mask_eroded

###########
#Filtering#
###########

#Create filter
switch ($reg)
	case "despike":
		fugue --loadfmap=${outroot}field --savefmap=${outroot}field_filter \
		--mask=${outroot}mag_brain_mask --despike --despikethreshhold=$dthresh
		breaksw
	case "smooth":
		fugue --loadfmap=${outroot}field --savefmap=${outroot}field_filter \
		--mask=${outroot}mag_brain_mask -s $mm3
		breaksw
	case "median":
		fugue --loadfmap=${outroot}field --savefmap=${outroot}field_filter \
		--mask=${outroot}mag_brain_mask -m
		breaksw
	case "none":
		imcp ${outroot}field ${outroot}field_filter
endsw

#Apply the filter to brain edges
fslmaths ${outroot}field_filter -sub ${outroot}field -mas ${outroot}mag_brain_mask_eroded -add \
		 ${outroot}field ${outroot}field

#Shift median to 0
set median = `fslstats ${outroot}field -k ${outroot}mag_brain_mask -P 50`
fslmaths ${outroot}field -sub $median ${outroot}field

#Estimate signal loss from fieldmap. Range goes from 0 (no signal) to 1 (full signal)
sigloss -i ${outroot}field --te=${te} -m ${outroot}mag_brain_mask -s ${outroot}signal_loss

#Multiply mag brain by signal loss image. Will result in a mag brain with areas of signal loss.
fslmaths ${outroot}signal_loss -mul ${outroot}mag_brain ${outroot}mag_brain_signal_lossed -odt float

#####################################
#FUGUE, Registration, Transformation#
#####################################

#Run fugue on the signal lossed magnitude image. Will distort it according to the field map. 
#Then do the same thing for the signal_loss fmap image.
fugue -i ${outroot}mag_brain_signal_lossed --loadfmap=${outroot}field \
	  --mask=${outroot}mag_brain_mask -w ${outroot}mag_brain_signal_lossed_distorted \
	  --nokspace --unwarpdir=$dir --dwell=$dwell 
fugue -i ${outroot}signal_loss --loadfmap=${outroot}field --mask=${outroot}mag_brain_mask \
	  --dwell=$dwell -w ${outroot}signal_loss_distorted --nokspace --unwarpdir=$dir

#Threshhold the distorted signal loss brain according to user chosen signal loss threshhold
fslmaths ${outroot}signal_loss_distorted -thr $thresh ${outroot}signal_loss_distorted

#Register the distorted magnitude to the distorted functional. Output a transformation file only.
#Use the threshholded signal loss brain as weighting (areas with 0 will be ignored)
flirt -in ${outroot}mag_brain_signal_lossed_distorted -ref ${outroot}epi_${frame}_brain \
	  -omat ${outroot}mag_brain_signal_lossed_distorted_to_${outroot}epi_${frame}_brain.mat -dof 6 \
	  -inweight ${outroot}signal_loss_distorted

#Apply the created transform to the field map and brain mask
flirt -in ${outroot}field -ref ${outroot}epi_${frame}_brain \
	  -init ${outroot}mag_brain_signal_lossed_distorted_to_${outroot}epi_${frame}_brain.mat \
	  -applyxfm -out ${outroot}field_to_${outroot}epi_${frame}_brain
flirt -in ${outroot}mag_brain_mask -ref ${outroot}epi_${frame}_brain \
	  -init ${outroot}mag_brain_signal_lossed_distorted_to_${outroot}epi_${frame}_brain.mat \
	  -applyxfm -out ${outroot}mag_brain_mask_to_${outroot}epi_${frame}_brain \
	  -interp nearestneighbour

#Run fugue using the registerted field map on the distorted epi. 
#Will undistort the first frame epi and save the shift map that does this.
fugue --loadfmap=${outroot}field_to_${outroot}epi_${frame}_brain \
	  --dwell=$dwell --mask=${outroot}mag_brain_mask_to_${outroot}epi_${frame}_brain \
	  -i ${outroot}epi_${frame}_brain -u ${outroot}epi_${frame}_brain_undistorted --unwarpdir=$dir \
	  --saveshift=${outroot}epi_${frame}_brain_undistorted_shift

#Convert the shiftwarp to an absolute warp
convertwarp -s ${outroot}epi_${frame}_brain_undistorted_shift \
		    -o ${outroot}epi_${frame}_brain_undistorted_shift_warp -r ${outroot}epi_${frame}_brain \
	        --shiftdir=$dir
	        
###################
#Motion Correction#
###################

if ( $?dtimc == 1  || $?boldmc == 1) then
	fslsplit ${outroot}epi ${outroot}epi_tmp
	foreach tmp ( ${outroot}epi_tmp* )
		
		#Create correct pre matrices
		if ( $?dtimc == 1 ) then
			set tmp = `remove_ext $tmp`
			echo $tmp > eddycorrect.log
			flirt -in $tmp -ref ${outroot}epi_${frame} -nosearch -omat ${tmp}.mat -paddingsize 1 \
			>> eddycorrect.log
			set pre = "${tmp}.mat"
		else
			if ( $?notfirst == 0 ) then
				mcflirt -refvol ${outroot}epi_${frame} -mats -in ${outroot}epi
			endif
			set notfirst = 1
			set tmp = `remove_ext $tmp`
			set num = `echo $tmp | grep -o "tmp.*[0-9]" | sed 's/tmp//g'`
			set pre = "${outroot}epi_mcf.mat/MAT_${num}"
		endif
		
		#Apply motion correction
		applywarp -i $tmp -o ${tmp}_undistorted_mc -r ${outroot}epi --abs \
				  --mask=${outroot}mag_brain_mask_to_${outroot}epi_${frame}_brain \
		          -w ${outroot}epi_${frame}_brain_undistorted_shift_warp --premat=$pre
	end
	
	fslmerge -t ${outroot}epi_undistorted_mc ${outroot}epi_tmp*_undistorted_mc.nii.gz
	rm ${outroot}epi_tmp*	
endif
		